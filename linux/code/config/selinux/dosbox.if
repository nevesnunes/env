
## <summary>policy for dosbox</summary>

########################################
## <summary>
##	Execute dosbox_exec_t in the dosbox domain.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`dosbox_domtrans',`
	gen_require(`
		type dosbox_t, dosbox_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, dosbox_exec_t, dosbox_t)
')

######################################
## <summary>
##	Execute dosbox in the caller domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`dosbox_exec',`
	gen_require(`
		type dosbox_exec_t;
	')

	corecmd_search_bin($1)
	can_exec($1, dosbox_exec_t)
')

########################################
## <summary>
##	Execute dosbox in the dosbox domain, and
##	allow the specified role the dosbox domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to be allowed the dosbox domain.
##	</summary>
## </param>
#
interface(`dosbox_run',`
	gen_require(`
		type dosbox_t;
		attribute_role dosbox_roles;
	')

	dosbox_domtrans($1)
	roleattribute $2 dosbox_roles;
')

########################################
## <summary>
##	Role access for dosbox
## </summary>
## <param name="role">
##	<summary>
##	Role allowed access
##	</summary>
## </param>
## <param name="domain">
##	<summary>
##	User domain for the role
##	</summary>
## </param>
#
interface(`dosbox_role',`
	gen_require(`
		type dosbox_t;
		attribute_role dosbox_roles;
	')

	roleattribute $1 dosbox_roles;

	dosbox_domtrans($2)

	ps_process_pattern($2, dosbox_t)
	allow $2 dosbox_t:process { signull signal sigkill };
')
